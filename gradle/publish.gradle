apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'maven-publish'

// Generate sources
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// generate javadocs
task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Upload to bintray
ext.bintrayUsername = hasProperty("bintrayUsername") ? bintrayUsername : ""
ext.bintrayApiKey = hasProperty("bintrayApiKey") ? bintrayApiKey : ""

publishing {
    publications {
        MvnPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'static-code-analysis-plugin'
            version project.version
        }
    }
}

bintray {
    user = project.ext.bintrayUsername
    key = project.ext.bintrayApiKey
    publish = false
    dryRun = true
    pkg {
        repo = 'monits-android'
        name = 'static-code-analysis-plugin'
        userOrg = 'monits'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/Monits/static-code-analysis-plugin/issues'
        vcsUrl = 'https://github.com/Monits/static-code-analysis-plugin.git'
        version {
            name = project.version
            desc = 'StaticCodeAnalysis plugin'
            released = new Date()
            vcsTag = project.version
        }
    }
    publications = ['MvnPublication']
}

// Gradle plugin portal
pluginBundle {
    website = 'https://github.com/Monits/static-code-analysis-plugin'
    vcsUrl = 'https://github.com/Monits/static-code-analysis-plugin.git'
    description = 'Configures PMD, CPD, Checkstyle and Findbugs out of the box for Android and Java projects beyond defaults to improve performance and extensibility, while allowing to use remote (cacheable) configuration'
    tags = ['code quality', 'static analysis', 'java', 'android', 'findbugs', 'fb-contrib', 'pmd', 'cpd', 'checkstyle', 'linter']

    plugins {
        scaPlugin {
            id = 'com.monits.staticCodeAnalysis'
            displayName = 'Fast and easy Static Code Analysis for Java and Android'
        }
    }

    mavenCoordinates {
        groupId = 'com.monits'
        artifactId = 'static-code-analysis-plugin'
    }
}

// Helper task to just upload everything everywhere
tasks['publish'].finalizedBy 'bintrayUpload', 'publishPlugins'
